# Maintainer: Torsten Keßler <t dot kessler at posteo dot de>
# Contributor: Jakub Okoński <jakub@okonski.org>
# Contributor: wuxxin <wuxxin@gmail.com>
pkgname=rocrand
pkgver=5.1.1
pkgrel=2
pkgdesc='Pseudo-random and quasi-random number generator on ROCm'
arch=('x86_64')
url='https://rocmdocs.amd.com/en/latest/ROCm_Libraries/ROCm_Libraries.html#rocrand'
license=('MIT')
depends=('hip')
makedepends=('cmake' 'git' 'gcc-fortran' 'python')
optdepends=('gcc-fortran: Use Fortran wrapper'
            'python: Use Python wrapper')
_rocrand='https://github.com/ROCmSoftwarePlatform/rocRAND'
_hiprand='https://github.com/ROCmSoftwarePlatform/hipRAND'
_roccommit=512ae4697f64f762abd8783c269cd3438a2daed1
_hipcommit=12e2f070337945318295c330bf69c6c060928b9e
source=("$pkgname-$pkgver.tar.gz::$_rocrand/archive/$_roccommit.tar.gz"
        "$pkgname-hiprand-$pkgver.tar.gz::$_hiprand/archive/$_hipcommit.tar.gz")
sha256sums=('0cf0c041a2f250d001fc8e17d77aa3a5f626301a187c270ef851c8dd4fb442ad'
            '6a891b7a420ee629776b2eca792b0f0cf92a35e99f7046515ecc9fd2359eadd3')
_rocname="$(basename "$_rocrand")-$(basename "${source[0]}" ".tar.gz")"
_hipname="$(basename "$_hiprand")-$(basename "${source[1]}" ".tar.gz")"

prepare() {
  rm -r "$srcdir/$_rocname/hipRAND"
  ln -sf "$srcdir/$_hipname" "$srcdir/$_rocname/hipRAND"
}

build() {
  local cmake_args=('-DCMAKE_INSTALL_PREFIX=/opt/rocm'
		                '-DBUILD_TEST=1'
                    )
  if [[ -n "$AMDGPU_TARGETS" ]]; then
    cmake_args+=("-DAMDGPU_TARGETS=${AMDGPU_TARGETS}")
  fi

  # -fcf-protection is not supported by HIP, see
  # https://github.com/ROCm-Developer-Tools/HIP/blob/rocm-5.1.x/docs/markdown/clang_options.md
  CXX=/opt/rocm/hip/bin/hipcc \
  CXXFLAGS="${CXXFLAGS} -fcf-protection=none" \
  cmake -Wno-dev -S "$_rocname" \
        "${cmake_args[@]}"
  make
}

check() {
	ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" make install

  install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/rocrand.conf" << EOF
/opt/rocm/hiprand/lib
/opt/rocm/rocrand/lib
EOF
  install -Dm644 "$_rocname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Some packages search for {roc,hip}rand includes in /opt/rocm/include/{roc,hip}rand
  install -d "$pkgdir/opt/rocm/include"
  ln -s "/opt/rocm/hiprand/include/" "$pkgdir/opt/rocm/include/hiprand"
  ln -s "/opt/rocm/rocrand/include/" "$pkgdir/opt/rocm/include/rocrand"
}
