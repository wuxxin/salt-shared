# Maintainer: wuxxin <wuxxin@gmail.com>
# Based on python-torchaudio; original contributors:
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=python-torchaudio-rocm
_pkgname=audio
pkgver=0.11.0
pkgrel=1
pkgdesc="Data manipulation and transformation for audio signal processing, powered by PyTorch (with ROCM GPU support)"
arch=('any')
url="https://github.com/pytorch/audio"
license=('BSD')
depends=('python' 'sox' 'python-pytorch-rocm')
optdepends=('python-kaldi-io')
makedepends=('git' 'python-setuptools' 'cmake' 'ninja' 'rocm-hip-sdk' 'miopen')
conflicts=('python-torchaudio' 'python-torchaudio-git')
provides=('python-torchaudio')
source=(
  "${_pkgname}-${pkgver}::git+${url}.git#tag=v${pkgver}"
#  "${pkgname}-kenlm::git+https://github.com/kpu/kenlm"
#  "${pkgname}-kaldi::git+https://github.com/kaldi-asr/kaldi"
)
sha256sums=(
  'SKIP'
)

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed -i -E '/^[[:space:]]+_fetch_third_party_libraries()/d' setup.py
  #git submodule init
  #git config submodule."kaldi".url "${srcdir}/${pkgname}"-kaldi
  #git config submodule."third_party/kenlm/submodule".url "${srcdir}/${pkgname}"-kenlm
  #git submodule update --init --recursive
}

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  BUILD_SOX=0 BUILD_KALDI=0 BUILD_CTC_DECODER=0 USE_ROCM=1 python setup.py build
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  BUILD_SOX=0 BUILD_KALDI=0 BUILD_CTC_DECODER=0 USE_ROCM=1 python setup.py install --root="$pkgdir"/ --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
